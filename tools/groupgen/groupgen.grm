AP -> Adj<rt,gram='полн'>;
AP -> Verb<rt,gram='прич,полн'>;
AP -> Adv AP<rt,gram='полн'>;
AP -> AP<rt,gnc-agr[1],gram='полн'> Word<wff=/и/> AP<gnc-agr[1],gram='полн'>;

WordLat -> Word<wff=/[a-zA-Z0-9\-]+/>;
WordNum -> Word<wff=/[0-9\.]+/>;

NPn -> Noun<rt> WordNum;
NPn -> WordNum Noun<rt>;
NPn -> Noun<rt> WordLat+;
NPn -> WordLat+ Noun<rt>;

NPa1 -> Noun<gram='~сокр'>;
NPa2 -> Noun<gram='~сокр'>;
NPa3 -> Noun<gram='~сокр'>;
NPa1 -> AP<gnc-agr[1],gram='вин,муж,неод'> NPa1<rt,gnc-agr[1],gram='вин,муж,неод'>;
NPa2 -> AP<gnc-agr[1],gram='вин,муж,од'> NPa2<rt,gnc-agr[1],gram='вин,муж,од'>;
NPa3 -> AP<gnc-agr[1]> NPa3<rt,gnc-agr[1]>;
NPa4 -> Noun<rt,gnc-agr[1],gram='~сокр'> Adj<gnc-agr[1]>;
NPa5 -> Noun<rt,gram='им'> Noun<gram='им'>;

NPc -> NP<rt> PP;
NPc -> NP<rt> NPa<gram='род'>;

NP -> NPa<gnc-agr[1]> Word<wff=/и/> NPa<gnc-agr[1]>;

NPa -> NPa1 | NPa2 | NPa3;
NP -> NPn | NPa | NPc;

PPl -> Prep<rt,wff=/(в|на|о|об|по|при)/> NP<gram='пр'>;
PPa -> Prep<rt,wff=/(в|включая|за|исключая|на|о|по|под|про|с|сквозь|через)/> NP<gram='вин'>;
PPg -> Prep<rt,wff=/(без|вблизи|ввиду|вместо|вне|внутрь|возле|вокруг|вроде|для|до|из|из-за|из-под|касательно|кроме|мимо|накануне|ниже|около|от|относительно|подле|позади|помимо|поперек|после|посреди|посредством|прежде|против|путем|ради|с|снаружи|среди|у)/> NP<gram='род'>;
PPd -> Prep<rt,wff=/(благодаря|вопреки|к|навстречу|наперекор|по|подобно|согласно)/> NP<gram='дат'>;
PPi -> Prep<rt,wff=/(за|кверх|между|над|перед|под|с)/> NP<gram='твор'>;
PP -> PPl | PPa | PPg | PPd | PPi;

VP -> Verb<gram='изъяв'>;
VP -> Adv VP<rt>;
VP -> VP<rt> Adv;

VP -> VP<rt> NP<gram='вин'>;
VP  -> NP<gram='вин'> VP<rt>;

QP -> AdvPron (Adv) Verb<rt> (Adv);
QP -> QP NP<gram='вин'>;

Group -> NP | VP | QP | PP | AP | NPa4 | NPa5;

SENT -> AnyWord SENT;
SENT -> Group SENT;
SENT -> AnyWord;
SENT -> Group;

Z -> SENT;

