#if 0
%%{
    machine PatternsCommon;

    alphtype unsigned short;

    include Symbols "symbols.rl6";

    action Reject  { Reject(); }

    action H { Hold(); }
    action R { Ret();  }

    action I { In();  }
    action O { Out(); }

    action Jan { SetMon(1); }
    action Feb { SetMon(2); }
    action Mar { SetMon(3); }
    action Apr { SetMon(4); }
    action May { SetMon(5); }
    action Jun { SetMon(6); }
    action Jul { SetMon(7); }
    action Aug { SetMon(8); }
    action Sep { SetMon(9); }
    action Oct { SetMon(10); }
    action Nov { SetMon(11); }
    action Dec { SetMon(12); }

    action D { AddMeaning<SS_NUM>(); }
    action T { AddMeaning<SS_TIME>(); }
    action J { AddMeaning<SS_JUNK>(); }

    sepa = (ndiglet|0) >I%O;
    sepe = sepa        >H;

    dsepa = (ndigit|0) >I%O;
    dsepe = dsepa      >H;

    spa = (sp|letter|0) >I%O;
    spe = spa           >H;

    digit_1   = digit{1}   >I%O%D;
    digit_2   = digit{2}   >I%O%D;
    digit_4   = digit{4}   >I%O%D;
    digit_8x  = digit{8,}  >I%O%D;

    digit_2_1   = (digit_2 | digit_1) %(digit_2_1,0)$(digit_2_1,1);
    digit_4_2   = (digit_4 | digit_2) %(digit_4_2,0)$(digit_4_2,1);
    digit_4_2_1 = (digit_4 | digit_2 | digit_1) %(digit_4_2_1,0)$(digit_4_2_1,1);

    joiner_common = [ ./\-];

    day_suff_common   = da_ap_sp? ('st'|'nd'|'rd'|'th');
    month_suff_common = sp? '.';
    year_suff_common  = da_ap_sp? 'y' ('.' | 'ear')?;

    d_m_joiner_common = sp? joiner_common sp?;
    m_d_joiner_common = sp? (',' | '>' '>'? | joiner_common) sp?;

    dm_y4_joiner_common = sp? ("'" | ',' | joiner_common) sp?;
    md_y4_joiner_common = sp? (',' | joiner_common) sp?;

    y4_dm_joiner_common = sp? ([,./\-]) sp?;
    y4_md_joiner_common = sp? ([ ,./\-] | '>' '>'?) sp?;

    dm_y2_joiner_common = sp? ("'" | joiner_common) sp?;
    y2_md_joiner_common = sp? (',' | sp) sp?;

    date_time_joiner_common = sp? [ ,@/\-] sp?;

    time_suff_common = sp? ('am'|'pm'|'a.' sp? 'm.'|'p.' sp? 'm.');

    hours_suff_common   = sp? 'h' ('.' | 'our' 's'?)?;
    minutes_suff_common = sp? 'm' ('.' | 'in' ('ute' 's'?)?)?;
    seconds_suff_common = sp? 's' ('.' | 'ec' ('ond' 's'?)?)?;

    t_joiner_dig    = sp? ':' sp?;
    t_hm_joiner_common = sp? [/.\-] sp?;
    t_ms_joiner_common = sp;

    comma_range_joiner = sp? ',' sp?;
    space_range_joiner = sp;
    dash_range_joiner = sp? '-' sp?;

    d_m_range_joiner_common = sp;
    m_d_range_joiner_common = sp;
    m_y_range_joiner_common = sp;
    d_d_range_joiner_common = dash_range_joiner;
    m_m_range_joiner_common = comma_range_joiner | dash_range_joiner | space_range_joiner;
    y_y_range_joiner_common = comma_range_joiner | dash_range_joiner;

}%%
#endif
