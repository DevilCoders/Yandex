#if 0
%%{
    machine Data;

    include PatternsCommon    "../pat_common.rl6";

    end_y_ya_e = cyr_y | cyr_Ya | cyr_E | cyr_Yu;
    end_a_e_u = cyr_A | cyr_E | cyr_U;

    jan = (cyr_Ya cyr_N cyr_V %Jan)
        | (cyr_Ya cyr_N cyr_V cyr_A cyr_R end_y_ya_e %Jan);

    feb = (cyr_F cyr_E cyr_V cyr_R? %Feb)
        | (cyr_F cyr_E cyr_V cyr_R cyr_A cyr_L end_y_ya_e %Feb);

    mar = (cyr_M cyr_A cyr_R %Mar)
        | (cyr_M cyr_A cyr_R cyr_T end_a_e_u? %Mar);

    apr = (cyr_A cyr_P cyr_R %Apr)
        | (cyr_A cyr_P cyr_R cyr_E cyr_L end_y_ya_e %Apr);

    may = (cyr_M cyr_A (cyr_J | cyr_Ya | cyr_E | cyr_Yu) %May);

    jun = (cyr_I cyr_Yu cyr_N %Jun)
        | (cyr_I cyr_Yu cyr_N end_y_ya_e %Jun);

    jul = (cyr_I cyr_Yu cyr_L %Jul)
        | (cyr_I cyr_Yu cyr_L end_y_ya_e %Jul);

    aug = (cyr_A cyr_V cyr_G %Aug)
        | (cyr_A cyr_V cyr_G cyr_U 'c' cyr_T end_a_e_u? %Aug);

    sep = ('c' cyr_E cyr_N cyr_T? %Sep)
        | ('c' cyr_E cyr_N cyr_T  cyr_Ya cyr_B cyr_R end_y_ya_e %Sep);

    oct = (cyr_O cyr_K cyr_T %Oct)
        | (cyr_O cyr_K cyr_T cyr_Ya cyr_B cyr_R end_y_ya_e %Oct);

    nov = (cyr_N cyr_O cyr_Ya cyr_B? %Nov)
        | (cyr_N cyr_O cyr_Ya cyr_B  cyr_R end_y_ya_e %Nov);

    dec = (cyr_D cyr_E cyr_K %Dec)
        | (cyr_D cyr_E cyr_K cyr_A cyr_B cyr_R end_y_ya_e %Dec);

    month_name = (jan | feb | mar | apr | may | jun | jul | aug | sep | oct | nov | dec) >I%O %(month_name,0)$(month_name,1);

    number_suff      = cyr_O? (cyr_E | cyr_G cyr_O);
    month_word       = cyr_M cyr_E 'c' ('.' | cyr_Ya cyr_C (cyr_A | cyr_E | cyr_U)?)?;
    year_word        = cyr_G (cyr_G? '.'? | cyr_O cyr_D (cyr_A | cyr_U)?);

    day_pref         = zlen;
    day_suff         = (day_suff_common | da_ap_sp? number_suff) >I%O;

    month_pref       = (month_word dash_sp?) >I%O;
    month_suff       = (month_suff_common? dash_sp? month_word) >I%O;

    year_pref        = zlen;
    year_suff        = (year_suff_common | da_ap_sp? number_suff | dash_sp? year_word | da_ap_sp? number_suff sp year_word) >I%O;

    d_m_joiner       = (d_m_joiner_common | sp? ',' sp?) >I%O;
    m_d_joiner       = m_d_joiner_common >I%O;

    dm_y4_joiner     = dm_y4_joiner_common >I%O;
    md_y4_joiner     = md_y4_joiner_common >I%O;

    y4_dm_joiner     = y4_dm_joiner_common >I%O;
    y4_md_joiner     = y4_md_joiner_common >I%O;

    y2_md_joiner     = y2_md_joiner_common >I%O;
    dm_y2_joiner     = dm_y2_joiner_common >I%O;

    date_time_joiner = date_time_joiner_common;

    time_pref        = zlen;
    time_suff        = time_suff_common;

    hours_suff       = hours_suff_common   | sp? cyr_Ch ('.' | cyr_A 'c'   (cyr_A | cyr_O cyr_V)?)?;
    minutes_suff     = minutes_suff_common | sp? cyr_M  ('.' | cyr_I cyr_N ('.'   | cyr_U cyr_T (cyr_Yy | cyr_A)?)?)?;
    seconds_suff     = seconds_suff_common | sp? 'c'    ('.' | cyr_E cyr_K ('.'   | cyr_U cyr_N cyr_D (cyr_Yy | cyr_A)?)?)?;

    t_hm_joiner      = t_hm_joiner_common  | sp? cyr_I sp?;
    t_ms_joiner      = t_ms_joiner_common;

    d_m_range_joiner = d_m_range_joiner_common >I%O;
    m_d_range_joiner = m_d_range_joiner_common >I%O;
    m_y_range_joiner = m_y_range_joiner_common >I%O;
    d_d_range_joiner = (d_d_range_joiner_common | sp (cyr_P cyr_O | cyr_D cyr_O | cyr_I) sp) >I%O;
    m_m_range_joiner = (m_m_range_joiner_common | sp (cyr_P cyr_O | cyr_D cyr_O | cyr_I) sp) >I%O;
    y_y_range_joiner = (y_y_range_joiner_common | sp (cyr_P cyr_O | cyr_D cyr_O | cyr_I) sp) >I%O;

}%%
#endif
